MODULE TEST_SNLS1Q_PF
  USE service, ONLY : SP
  USE funit
  IMPLICIT NONE

CONTAINS
  @test
  SUBROUTINE SNLS1Q()
    !> Quick check for SNLS1E, SNLS1 and SCOV.
    !***
    ! **Description:**
    !
    !   This subroutine performs a quick check on the subroutines SNLS1E
    !   (and SNLS1) and SCOV.

    USE service, ONLY : eps_sp
    USE approximation, ONLY : FDJAC3, SCOV, SNLS1E
    !     .. Local Scalars ..
    REAL(SP) :: fnorm, fnorms, sigma, temp1, temp2, temp3, tol, tol2
    INTEGER :: i, iflag, info, infos, iopt, ldfjac, lwa, m, n, nprint
    !     .. Local Arrays ..
    REAL(SP) :: fjac(10,2), fjrow(2,1), fjtj(3), fvec(10), wa(40), x(2)
    INTEGER :: iw(2)
    !* FIRST EXECUTABLE STATEMENT  SNLS1Q
    infos = 1
    fnorms = 1.1151779E+01_SP
    m = 10
    n = 2
    lwa = 40
    ldfjac = 10
    nprint = -1
    iflag = 1
    tol = SQRT(40._SP*eps_sp)
    tol2 = SQRT(tol)
    !
    !  OPTION=2, the full Jacobian is stored and the user provides the Jacobian.
    !
    iopt = 2
    x(1) = 3.0E-1_SP
    x(2) = 4.0E-1_SP
    CALL SNLS1E(FCN2,iopt,m,n,x,fvec,tol,nprint,info,iw,wa,lwa)
    fnorm = NORM2(fvec)
    @AssertEqual( infos, info )
    @AssertRelativelyEqual( fnorms, fnorm, tol )
    !
    !     Form JAC-transpose*JAC.
    !
    sigma = fnorm*fnorm/(m-n)
    iflag = 2
    CALL FCN2(iflag,m,n,x,fvec,fjac,ldfjac)
    DO i = 1, 3
      fjtj(i) = 0._SP
    END DO
    DO i = 1, m
      fjtj(1) = fjtj(1) + fjac(i,1)**2
      fjtj(2) = fjtj(2) + fjac(i,1)*fjac(i,2)
      fjtj(3) = fjtj(3) + fjac(i,2)**2
    END DO
    !
    !     Calculate the covariance matrix.
    !
    CALL SCOV(FCN2,iopt,m,n,x,fvec,fjac,ldfjac,info,wa(1),wa(n+1),wa(2*n+1),wa(3*n+1))
    !
    !     Form JAC-transpose*JAC * covariance matrix (should = SIGMA*I).
    !
    temp1 = (fjtj(1)*fjac(1,1)+fjtj(2)*fjac(1,2))/sigma
    temp2 = (fjtj(1)*fjac(1,2)+fjtj(2)*fjac(2,2))/sigma
    temp3 = (fjtj(2)*fjac(1,2)+fjtj(3)*fjac(2,2))/sigma
    @AssertEqual( infos, info )
    @AssertEqual( 1._SP, temp1, tol2 )
    @AssertEqual( 0._SP, temp2, tol2 )
    @AssertEqual( 1._SP, temp3, tol2 )
    !
    !     OPTION=1, the full Jacobian is stored and the code approximates
    !     the Jacobian.
    !
    iopt = 1
    x(1) = 3.0E-1_SP
    x(2) = 4.0E-1_SP
    CALL SNLS1E(FCN1,iopt,m,n,x,fvec,tol,nprint,info,iw,wa,lwa)
    fnorm = NORM2(fvec)
    @AssertEqual( infos, info )
    @AssertRelativelyEqual( fnorms, fnorm, tol )
    !
    !     Form JAC-transpose*JAC.
    !
    sigma = fnorm*fnorm/(m-n)
    iflag = 1
    CALL FDJAC3(FCN1,m,n,x,fvec,fjac,ldfjac,iflag,0._SP,wa)
    DO i = 1, 3
      fjtj(i) = 0._SP
    END DO
    DO i = 1, m
      fjtj(1) = fjtj(1) + fjac(i,1)**2
      fjtj(2) = fjtj(2) + fjac(i,1)*fjac(i,2)
      fjtj(3) = fjtj(3) + fjac(i,2)**2
    END DO
    !
    !     Calculate the covariance matrix.
    !
    CALL SCOV(FCN1,iopt,m,n,x,fvec,fjac,ldfjac,info,wa(1),wa(n+1),wa(2*n+1),wa(3*n+1))
    !
    !     Form JAC-transpose*JAC * covariance matrix (should = SIGMA*I).
    !
    temp1 = (fjtj(1)*fjac(1,1)+fjtj(2)*fjac(1,2))/sigma
    temp2 = (fjtj(1)*fjac(1,2)+fjtj(2)*fjac(2,2))/sigma
    temp3 = (fjtj(2)*fjac(1,2)+fjtj(3)*fjac(2,2))/sigma
    @AssertEqual( infos, info )
    @AssertEqual( 1._SP, temp1, tol2 )
    @AssertEqual( 0._SP, temp2, tol2 )
    @AssertEqual( 1._SP, temp3, tol2 )
    !
    !  OPTION=3, the full Jacobian is not stored. Only the product of the Jacobian
    !  transpose and Jacobian is stored. The user provides the Jacobian one row at a time.
    !
    iopt = 3
    x(1) = 3.0E-1_SP
    x(2) = 4.0E-1_SP
    CALL SNLS1E(FCN3,iopt,m,n,x,fvec,tol,nprint,info,iw,wa,lwa)
    fnorm = NORM2(fvec)
    @AssertEqual( infos, info )
    @AssertRelativelyEqual( fnorms, fnorm, tol )
    !
    !     Form JAC-transpose*JAC.
    !
    sigma = fnorm*fnorm/(m-n)
    DO i = 1, 3
      fjtj(i) = 0._SP
    END DO
    iflag = 3
    DO i = 1, m
      CALL FCN3(iflag,m,n,x,fvec,fjrow,i)
      fjtj(1) = fjtj(1) + fjrow(1,1)**2
      fjtj(2) = fjtj(2) + fjrow(1,1)*fjrow(2,1)
      fjtj(3) = fjtj(3) + fjrow(2,1)**2
    END DO
    !
    !     Calculate the covariance matrix.
    !
    CALL SCOV(FCN3,iopt,m,n,x,fvec,fjac,ldfjac,info,wa(1),wa(n+1),wa(2*n+1),wa(3*n+1))
    !
    !     Form JAC-transpose*JAC * covariance matrix (should = SIGMA*I).
    !
    temp1 = (fjtj(1)*fjac(1,1)+fjtj(2)*fjac(1,2))/sigma
    temp2 = (fjtj(1)*fjac(1,2)+fjtj(2)*fjac(2,2))/sigma
    temp3 = (fjtj(2)*fjac(1,2)+fjtj(3)*fjac(2,2))/sigma
    @AssertEqual( infos, info )
    @AssertEqual( 1._SP, temp1, tol2 )
    @AssertEqual( 0._SP, temp2, tol2 )
    @AssertEqual( 1._SP, temp3, tol2 )
    !
    RETURN
  END SUBROUTINE SNLS1Q
  !** FCN1
  PURE SUBROUTINE FCN1(Iflag,M,N,X,Fvec,Fjac,Ldfjac)
    !> Subsidiary to SNLS1Q.
    !***
    ! **Description:**
    !
    !   Subroutine which evaluates the function for test program
    !   used in quick check of SNLS1E.
    !
    !   Numerical approximation of Jacobian is used.

    !     .. Scalar Arguments ..
    INTEGER, INTENT(IN) :: Iflag, Ldfjac, M, N
    !     .. Array Arguments ..
    REAL(SP), INTENT(IN) :: X(N)
    REAL(SP), INTENT(INOUT) :: Fvec(M)
    REAL(SP), INTENT(OUT) :: Fjac(:,:)
    !     .. Local Scalars ..
    INTEGER :: i
    REAL(SP) :: temp
    !     .. Intrinsic Functions ..
    INTRINSIC EXP
    !* FIRST EXECUTABLE STATEMENT  FCN1
    IF( Iflag/=1 ) RETURN
    DO i = 1, M
      temp = i
      Fvec(i) = 2._SP + 2._SP*temp - EXP(temp*X(1)) - EXP(temp*X(2))
    END DO
  END SUBROUTINE FCN1
  !** FCN2
  PURE SUBROUTINE FCN2(Iflag,M,N,X,Fvec,Fjac,Ldfjac)
    !> Subsidiary to SNLS1Q.
    !***
    ! **Description:**
    !
    !   Subroutine to evaluate function and full Jacobian for test
    !   problem in quick check of SNLS1E.

    !     .. Scalar Arguments ..
    INTEGER, INTENT(IN) :: Iflag, Ldfjac, M, N
    !     .. Array Arguments ..
    REAL(SP), INTENT(IN) :: X(N)
    REAL(SP), INTENT(INOUT) :: Fvec(M)
    REAL(SP), INTENT(OUT) :: Fjac(:,:)
    !     .. Local Scalars ..
    REAL(SP) :: temp
    INTEGER :: i
    !     .. Intrinsic Functions ..
    INTRINSIC EXP
    !* FIRST EXECUTABLE STATEMENT  FCN2
    IF( Iflag==0 ) RETURN
    !
    !     Should we evaluate functions or Jacobian?
    !
    IF( Iflag==1 ) THEN
      !
      !       Evaluate functions.
      !
      DO i = 1, M
        temp = i
        Fvec(i) = 2._SP + 2._SP*temp - EXP(temp*X(1)) - EXP(temp*X(2))
      END DO
    ELSE
      !
      !       Evaluate Jacobian.
      !
      IF( Iflag/=2 ) RETURN
      DO i = 1, M
        temp = i
        Fjac(i,1) = -temp*EXP(temp*X(1))
        Fjac(i,2) = -temp*EXP(temp*X(2))
      END DO
    END IF
  END SUBROUTINE FCN2
  !** FCN3
  PURE SUBROUTINE FCN3(Iflag,M,N,X,Fvec,Fjrow,Nrow)
    !> Subsidiary to SNLS1Q.
    !***
    ! **Description:**
    !
    !   Subroutine to evaluate the Jacobian, one row at a time, for
    !   test problem used in quick check of SNLS1E.

    !     .. Scalar Arguments ..
    INTEGER, INTENT(IN) :: Iflag, M, N, Nrow
    !     .. Array Arguments ..
    REAL(SP), INTENT(IN) :: X(N)
    REAL(SP), INTENT(INOUT) :: Fvec(M)
    REAL(SP), INTENT(OUT) :: Fjrow(:,:)
    !     .. Local Scalars ..
    REAL(SP) :: temp
    INTEGER :: i
    !     .. Intrinsic Functions ..
    INTRINSIC EXP
    !* FIRST EXECUTABLE STATEMENT  FCN3
    IF( Iflag==0 ) RETURN
    !
    !     Should we evaluate functions or Jacobian?
    !
    IF( Iflag==1 ) THEN
      !
      !       Evaluate functions.
      !
      DO i = 1, M
        temp = i
        Fvec(i) = 2._SP + 2._SP*temp - EXP(temp*X(1)) - EXP(temp*X(2))
      END DO
    ELSE
      !
      !       Evaluate one row of Jacobian.
      !
      IF( Iflag/=3 ) RETURN
      temp = Nrow
      Fjrow(1,1) = -temp*EXP(temp*X(1))
      Fjrow(2,1) = -temp*EXP(temp*X(2))
    END IF
  END SUBROUTINE FCN3
  !
END MODULE TEST_SNLS1Q_PF
